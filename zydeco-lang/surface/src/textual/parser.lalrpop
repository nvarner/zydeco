use crate::textual::{
    syntax::*, lexer::*, escape,
};

grammar<'input, 'ctx>(
    input: &'input str,
    ctx: &'ctx mut Ctx,
);

//* --------------------------------- Tokens --------------------------------- */

extern {
    type Location = usize;
    
    enum Tok<'input> {
        "UpperId" => Tok::UpperIdent(<&'input str>),
        "LowerId" => Tok::LowerIdent(<&'input str>),

        "where" => Tok::Where,
        "end" => Tok::End,
        "module" => Tok::Module,
        "use" => Tok::Use,
        "pub" => Tok::Pub,
        "extern" => Tok::Extern,
        "data" => Tok::Data,
        "codata" => Tok::Codata,
        "alias" => Tok::Alias,
        "define" => Tok::Define,
        "main" => Tok::Main,
        "let" => Tok::Let,
        "in" => Tok::In,
        "do" => Tok::Do,
        "ret" => Tok::Ret,
        "fn" => Tok::Fn,
        "pi" => Tok::Pi,
        "rec" => Tok::Rec,
        "match" => Tok::Match,
        "comatch" => Tok::Comatch,
        "forall" => Tok::Forall,
        "exists" => Tok::Exists,
        "@" => Tok::At,
        "pack" => Tok::Pack,

        "NumLit" => Tok::NumLit(<&'input str>),
        "StrLit" => Tok::StrLit(<&'input str>),
        "CharLit" => Tok::CharLit(<&'input str>),

        "(" => Tok::ParenOpen,
        ")" => Tok::ParenClose,
        "[" => Tok::BracketOpen,
        "]" => Tok::BracketClose,
        "{" => Tok::BraceOpen,
        "}" => Tok::BraceClose,
        "," => Tok::Comma,
        ":" => Tok::Colon,
        "::" => Tok::ColonColon,
        "=" => Tok::Equals,
        ";" => Tok::Semicolon,
        "!" => Tok::Force,
        "/" => Tok::Slash,
        "|" => Tok::Branch,
        "+" => Tok::Plus,
        "." => Tok::Dot,
        ".." => Tok::DotDot,
        "->" => Tok::Arrow,
        "<-" => Tok::Assign,
        "_" => Tok::Hole,
    }
}

//* -------------------------------- Utilities ------------------------------- */

#[inline]
Sp<T>: Sp<T> = <l:@L> <inner:T> <r:@R> => Sp { info: Span::new(l, r), inner, };

// #[inline]
SepByDelim<Start, Sep, T, End>: Vec<T> = {
    Start <mut v: (<T> Sep)*> <last: T?> End => {
        match last {
            None => { },
            Some(t) => { v.push(t); }
        };
        v
    }
};

//* -------------------------------- Terminals ------------------------------- */

Int: i64 = <s:"NumLit"> => s.parse().unwrap();
String: String = <s:"StrLit"> => escape::apply_string_escapes(&s[1..s.len()-1]);
Char: char = <s:"CharLit"> => escape::apply_char_escapes(s);

Literal: Literal = {
    <Int> => <>.into(),
    <String> => <>.into(),
    <Char> => <>.into(),
};

//* ------------------------------- Identifiers ------------------------------ */

#[inline]
UpperId: String = "UpperId" => <>.into();
#[inline]
LowerId: String = "LowerId" => <>.into();
#[inline]
MixedId: String = { <UpperId>, <LowerId>, };

#[inline]
ModName: ModName = <UpperId> => ModName(<>);
#[inline]
VarName: VarName = <MixedId> => VarName(<>);
CtorName: CtorName = {
    "+" <"UpperId"> => {
        let mut s = String::from("+"); s += <>; CtorName(s)
    },
    "pack" => CtorName(String::from("pack")),
};
DtorName: DtorName = {
    "." <"LowerId"> => {
        let mut s = String::from("."); s += <>; DtorName(s)
    },
    "@" => DtorName(String::from("@")),
};

NameDef<T>: NameDef<T> = <T> => NameDef(<>);
NameRef<T>: NameRef<T> = <path:(<ModName> "/")*> <inner:T> => NameRef(path, inner);

DefId: DefId = <NameDef<Sp<VarName>>> => ctx.def(<>.0);

//* --------------------------------- Pattern -------------------------------- */

#[inline]
PatternId: PatternId = <Sp<Pattern>> => ctx.pattern(<>);
Pattern: Pattern = {
    "(" <Pattern> ")" => <>.into(),
    <DefId> => <>.into(),
    "(" <term:PatternId> ":" <ty:TermId> ")" => Annotation {
        term, ty,
    }.into(),
    "_" => Hole.into(),
};

//* ---------------------------------- Term ---------------------------------- */

GenBind: GenBind = {
    <rec:"rec"?> <fun:"fn"?> <binder:PatternId> <params:PatternId*>
    <ty:(":" <TermId>)?> <bindee: ("=" <TermId>)?> => GenBind {
        rec: rec.is_some(),
        fun: fun.is_some(),
        binder, params, ty, bindee,
    }
};

#[inline]
LetSep: () = { ";", "in", };

Matcher: Matcher<TermId> = {
    "|" <name:CtorName>
        <binders:SepByDelim<"(", ",", <PatternId>, ")">>
    "->" <tail:TermId> => Matcher {
        name, binders, tail
    },
};

CoMatcher: CoMatcher<TermId> = {
    "|" <name:DtorName>
        <binders:SepByDelim<"(", ",", <PatternId>, ")">>
    "->" <tail:TermId> => CoMatcher {
        name, binders, tail
    },
};

#[inline]
TermGen<T>: TermId = <Sp<T>> => ctx.term(<>);

#[inline]
TermId: TermId = <TermGen<Term>>;
Term: Term<NameRef<VarName>> = {
    TermRight,
    // abstraction
    "fn" <params:PatternId*> "->"
        <term:TermId> => Abstraction(params, term).into(),
    // pi
    "pi" <params:PatternId*> "->"
        <term:TermId> => Pi(params, term).into(),
    // forall
    "forall" <params:PatternId*> "." <ty:TermRightId> =>
        Forall(params, ty).into(),
    // exists
    "exists" <params:PatternId*> "." <ty:TermRightId> =>
        Exists(params, ty).into(),
    // recursion
    "rec" <binder:PatternId> "->"
        <term:TermId> => Recursion(binder, term).into(),
    // bind
    "do" <binder:PatternId> "<-" <bindee:TermId> ";" <tail:TermId> => Bind {
        binder, bindee, tail
    }.into(),
    // pure bind
    "let" <binding:GenBind> LetSep <tail:TermId> => PureBind {
        binding, tail
    }.into(),
    // match
    "match" <scrut:TermId> <arms:Matcher*> "end" => Match {
        scrut, arms
    }.into(),
    "comatch" <arms:CoMatcher*> "end" => CoMatch {
        arms
    }.into(),
};

#[inline]
TermRightId: TermId = <TermGen<TermRight>>;
TermRight: Term<NameRef<VarName>> = {
    TermAtom,
    // application
    <term:TermRightId> <arg:TermAtomId> => Application(
        term, vec![arg]
    ).into(),
    // arrow
    <ty_in:TermRightId> "->" <ty_out:TermAtomId> => Arrow(
        ty_in, ty_out
    ).into(),
    // destructor
    <term:TermGen<TermRight>> <name:DtorName>
    <args:SepByDelim<"(", ",", <TermId>, ")">> => Destructor(
        term, name, args
    ).into(),
};

#[inline]
TermAtomId: TermId = <TermGen<TermAtom>>;
TermAtom: Term<NameRef<VarName>> = {
    "(" <Term> ")" => <>.into(),
    "(" <term:TermId> ":" <ty:TermId> ")" => Annotation {
        term, ty,
    }.into(),
    "_" => Hole.into(),
    <NameRef<VarName>> => Term::Var(<>),
    "{" <TermId> "}" => Thunk(<>).into(),
    "!" <TermAtomId> => Force(<>).into(),
    "ret" <TermAtomId> => Return(<>).into(),
    // constructor
    <name:CtorName> <args:SepByDelim<"(", ",", <TermId>, ")">> => Constructor(
        name, args
    ).into(),
    <Literal> => <>.into(),
};

pub SingleTerm: TermId = TermId;

//* -------------------------------- TopLevel -------------------------------- */

// #[inline]
TypeDef: TypeDef = {
    <head:TypeDefHead> <name:DefId> <params:PatternId*>
        <arms:("where" <TypeArm*>)?>
    "end" => TypeDef {
        head, name, params, arms,
    }
};
// #[inline]
TypeDefHead: TypeDefHead = {
    "data" => TypeDefHead::Data,
    "codata" => TypeDefHead::CoData,
};
// #[inline]
TypeArmName: TypeArmName = {
    <CtorName> => <>.into(),
    <DtorName> => <>.into(),
};
#[inline]
TypeArm: TypeArm = {
    "|" <name:TypeArmName> <args:SepByDelim<"(", ",", <TermId>, ")">>
    <out:(":" <TermId>)?> => TypeArm {
        name, args, out,
    },
};

#[inline]
DefineHead: () = { "define", "alias", };
#[inline]
DefineEnd: () = { "end", ";", };

UseEnum: UseEnum = {
    <VarName> => <>.into(),
    <binder:VarName> "=" <origin:VarName> => UseAlias(binder, origin).into(),
    ".." => UseAll.into(),
    <SepByDelim<"(", ",", UseDef, ")">> => UseCluster(<>).into(),
};
UseDef: UseDef = <NameRef<UseEnum>> => UseDef(<>);

Declaration: Declaration = {
    <TypeDef> => <>.into(),
    DefineHead <GenBind> DefineEnd => Define(<>).into(),
    "module" <name:NameDef<ModName>>
        <top:("where" <TopLevel>)?>
    "end" => Module { name, top }.into(),
    "use" <UseDef> "end" => <>.into(),
    "main" <TermId> "end" => Main(<>).into(),
};

Modifiers<T>: Modifiers<T> = {
    <public:"pub"?> <external:"extern"?> <inner:T> => Modifiers {
        public: public.is_some(),
        external: external.is_some(),
        inner,
    },
};

pub ReplInput: ReplInput = {
    <Modifiers<Declaration>> => <>.into(),
    <TermId> => <>.into(),
};

pub TopLevel: TopLevel = {
    <Modifiers<Declaration>*> => TopLevel(<>),
};
